zmodload zsh/datetime
start=$EPOCHREALTIME

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi


#
# Exports
#

export HISTFILE=~/.histfile
export HISTSIZE=1000
export SAVEHIST=1000
export EDITOR=micro
export GIT_EDITOR=micro
export RUSTC_WRAPPER=sccache
export ZSH_AUTOSUGGEST_USE_ASYNC=1
export ZSH_AUTOSUGGEST_STRATEGY=(history completion)
export GPG_TTY=$(tty)
export FZF_DEFAULT_COMMAND="rg --files --no-ignore-vcs --hidden"

{% if yadm.os == "Linux" %}
export WINEESYNC=1
export WINEFSYNC=1
export PAGER='moar'
export SYSTEMD_LESS='FRSMK'
export SYSTEMD_PAGER='moar'
{% endif %}


#
# Path
#

path+=("$HOME/.local/bin")
fpath+=("$HOME/.config/zsh/func")

{% if yadm.os == "Linux" %}
path+=('/usr/lib/ccache/bin/')
{% endif %}

{% if yadm.os == "Darwin" %}
export HOMEBREW_PREFIX="/usr/local"
export HOMEBREW_CELLAR="/usr/local/Cellar"
export HOMEBREW_REPOSITORY="/usr/local/Homebrew"
path+=("$HOMEBREW_PREFIX/bin" "$HOMEBREW_PREFIX/sbin" "$HOMEBREW_PREFIX/bin/ccache" "$HOMEBREW_PREFIX/opt/python/libexec/bin")
manpath+=("$HOMEBREW_PREFIX/share/man")
infopath+=("$HOMEBREW_PREFIX/share/info")
fpath+=("$HOMEBREW_PREFIX/share/zsh/site-functions")
{% endif %}


#
# Options
#

bindkey -e
setopt sharehistory autocd extendedglob append_history correct \
	   hist_ignore_all_dups hist_verify hist_ignore_space complete_aliases

# The following lines were added by compinstall
zstyle ':completion:*:git-checkout:*' sort false
zstyle ':completion:*' completer _expand _complete _ignored _correct _approximate
zstyle ':completion:*' matcher-list '' 'm:{[:lower:]}={[:upper:]} m:{[:lower:][:upper:]}={[:upper:][:lower:]}' 'l:|=* r:|=*'
zstyle ':completion::complete:*' gain-privileges 1
zstyle ':completion:*' menu select
zstyle ':completion:*:descriptions' format '[%d]'
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'exa -1 --color=always $realpath'

zsh_dir=$HOME/.config/zsh

autoload -Uz compinit edit-command-line $zsh_dir/func/*

# only compinit once a day
for dump in ~/.zcompdump(N.mh+24); do
  compinit
done

# zcompile .zshrc
zcompare ${ZDOTDIR:-${HOME}}/.zshrc

# zcompile the completion cache
zcompare ${ZDOTDIR:-${HOME}}/.zcompdump

# zcompile p10k config
zcompare ${ZDOTDIR:-${HOME}}/.p10k.zsh
zcompare "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"

# zcompile sourced scripts
for file in $zsh_dir/**/*.zsh; do
	zcompare "$file"
done

# zcompile autoloaded functions
for file in $zsh_dir/func/^(*.zwc)(.); do
	zcompare "$file"
done

compinit -C

exit_zsh() { exit }
zle -N exit_zsh
zle -N edit-command-line
zle -N history-substring-search-up
zle -N history-substring-search-down


#
# Sources
#

source "$zsh_dir/zsh-lazyload/zsh-lazyload.zsh"
source "$zsh_dir/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh"
source "$zsh_dir/zsh-autosuggestions/zsh-autosuggestions.zsh"
source "$zsh_dir/fzf-tab/fzf-tab.plugin.zsh"
source "$zsh_dir/zsh-history-substring-search/zsh-history-substring-search.zsh"
source "$zsh_dir/powerlevel10k/powerlevel10k.zsh-theme"
source "$zsh_dir/forgit/forgit.plugin.zsh"


#
# Aliases
#

alias ls='exa'
alias tree='exa --tree'
alias nano='micro'
alias cat='bat'

if [ -z ${DISPLAY+x} ] ; then
	alias gpg="gpg --pinentry-mode loopback "
fi

{% if yadm.os == "Linux" %}
alias cp='cp --reflink=auto'
alias tkg="bash $HOME/git/tkg/update.sh"
alias copy="xclip -selection c"
alias yay='paru'
alias whichpkg="pacman -Qo"
{% endif %}


#
# Bindings
#

bindkey '^X^e' edit-command-line
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down
bindkey '^D' exit_zsh


#
# rbenv
#

lazyload rbenv ruby rake bundle bundler gem irb -- 'eval "$(rbenv init -)"'

{% if yadm.os == "Linux" %}
#
# Completion rehash
#

TRAPUSR1() { rehash }

#
# Terminal title hooks
#

autoload -Uz add-zsh-hook

if [[ "$TERM" == (Eterm*|alacritty*|aterm*|gnome*|konsole*|kterm*|putty*|rxvt*|screen*|tmux*|xterm*|foot*) ]]; then
	add-zsh-hook -Uz precmd xterm_title_precmd
	add-zsh-hook -Uz preexec xterm_title_preexec
fi

{% endif %}


[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

let "dur = $EPOCHREALTIME - $start"
echo "Execution time: $dur seconds"
